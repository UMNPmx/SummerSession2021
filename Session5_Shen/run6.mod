;; 1. Based on:
;; 2. Description: PKPD model
;; x1. Author: user
;; 3. Label:

$PROBLEM PKPD

$INPUT C ID TIME NTIME DV AMT CMT EVID MDV TYPE
       GENO SEX ETHNICITY AGE WT HT CRCL DOSE BMI

$DATA r4pmx_dataset_simulation1.csv IGNORE=C

$SUBROUTINES ADVAN13 TOL=12
$MODEL
  COMP =(DEPOT)
  COMP =(TRANS1)
  COMP =(TRANS2)
  COMP =(TRANS3)
  COMP =(CENT)
  COMP =(RESP)

$PK

TVKTR  = THETA(1)
TVCL   = THETA(2)
TVVC   = THETA(3)

TVIC50 = THETA(4)
TVKIN  = THETA(5)
TVKOUT = THETA(6)

KTR    = TVKTR  * EXP(ETA(1))
CL     = TVCL   * EXP(ETA(2))
VC     = TVVC   * EXP(ETA(3))

IC50   = TVIC50 * EXP(ETA(4))
KIN    = TVKIN  * EXP(ETA(5))
KOUT   = TVKOUT * EXP(ETA(6))

S5=VC
S6=1

A_0(6) = KIN/KOUT


$DES
CP = A(5)/VC

DADT(1)= - KTR * A(1)
DADT(2)=   KTR * (A(1) - A(2))
DADT(3)=   KTR * (A(2) - A(3))
DADT(4)=   KTR * (A(3) - A(4))

DADT(5)=   KTR * A(4) - CL*CP

INH = CP/(IC50 + CP)

DADT(6)=   KIN*(1-INH) - KOUT*A(6)


$ERROR
CCP  = A(5)/S5
RESP = A(6)

IF (TYPE.EQ.1)IPRED = CCP
IF (TYPE.EQ.2)IPRED = RESP

IF (TYPE.EQ.1) Y   = IPRED + IPRED*ERR(1)
IF (TYPE.EQ.2) Y   = IPRED + IPRED*ERR(2)

$THETA
(0, 1)  ; KTR
(0, 1)  ;CL
(0, 20) ;VC
(0, 1)  ; IC50
(0, 50)  ; KIN
(0, 5) ; KOUT

$OMEGA
 0.1  ; IIV KTR
 
$OMEGA BLOCK(2)
 0.1       ; IIV CL
 0.05 0.2; IIV VC


$OMEGA
 0.3  ; IIV KOUT
 0.2  ; IIV KIN
 0 FIX; IIV KOUT


$SIGMA
 0.01 ; Prop ERR PK
 0.04 ; Prop ERR PD

$EST METHOD=1 INTER PRINT=5 MAX=9999 NSIG=3 SIGL=9 POSTHOC
$COV SIGL=12 MATRIX=R PRINT=E

$TABLE ID TIME DV MDV EVID IPRED PRED CWRES
KTR CL VC IC50 KIN KOUT ETA1 ETA2 ETA3
GENO SEX ETHNICITY AGE WT HT CRCL DOSE BMI
ONEHEADER NOPRINT FILE=sdtab006
