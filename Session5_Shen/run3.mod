;; 1. Based on: run2
;; 2. Description: PK model +GENO-CL +AGE-CL
;; x1. Author: user
;; 3. Label:

$PROBLEM PK

$INPUT C ID TIME NTIME DV AMT CMT EVID MDV TYPE
       GENO SEX ETHNICITY AGE WT HT CRCL DOSE BMI

$DATA r4pmx_dataset_simulation1.csv IGNORE=C  IGNORE(TYPE.EQ.2)

$SUBROUTINES ADVAN13 TOL=12
$MODEL
  COMP =(DEPOT)
  COMP =(TRANS1)
  COMP =(TRANS2)
  COMP =(TRANS3)
  COMP =(CENT)

$PK
TVKTR  = THETA(1)

TVCL   = THETA(2)*((WT/70)**0.75)*((AGE/50)**THETA(6))
IF (GENO.EQ.2) TVCL = TVCL*THETA(4)
IF (GENO.EQ.3) TVCL = TVCL*THETA(5)

TVVC   = THETA(3)*(WT/70)

KTR    = TVKTR * EXP(ETA(1))
CL     = TVCL  * EXP(ETA(2))  
VC     = TVVC  * EXP(ETA(3))


S5=VC

KEL=CL/VC


$DES
CP = A(5)/VC

DADT(1)= - KTR * A(1)
DADT(2)=   KTR * (A(1) - A(2))
DADT(3)=   KTR * (A(2) - A(3))
DADT(4)=   KTR * (A(3) - A(4))

DADT(5)=   KTR * A(4) - CL*CP


$ERROR
CCP   = A(5)/S5
IPRED = CCP
Y   = IPRED + IPRED*ERR(1)

$THETA
(0, 1)  ; KTR
(0, 1)  ; CL
(0, 20) ; VC

(0, 0.5) ;GENOCL2
(0, 0.2) ;GENOCL3

(-0.3)  ;AGECL

$OMEGA
 0.1  ; IIV KTR
 
$OMEGA BLOCK(2)
 0.1  ; IIV CL
 0.05 0.2  ; IIV VC


$SIGMA
 0.01 ; Prop ERR

$EST METHOD=1 INTER PRINT=5 MAX=9999 NSIG=3 SIGL=9 POSTHOC
$COV SIGL=12 MATRIX=R PRINT=E

$TABLE ID TIME DV MDV EVID IPRED PRED CWRES
KTR CL VC KEL ETA1 ETA2 ETA3
GENO SEX ETHNICITY AGE WT HT CRCL DOSE BMI
ONEHEADER NOPRINT FILE=sdtab003
